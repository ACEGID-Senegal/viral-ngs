"""
    This is a basic framework for processing of of viral genomes, currently
    tailored for EBOV. Some generalization work needed to expand this
    to generic viral genomes with an arbitrary number of segments/chromosomes.
    
    Make copies of this Snakefile and config.json to your analysis directory and
    customize as needed.
"""

__author__ = 'Daniel Park <dpark@broadinstitute.org>'


configfile: "config.json"

include: config["binDir"]+"/pipes/rules/common.rules"

include: config["binDir"]+"/pipes/rules/demux.rules"
include: config["binDir"]+"/pipes/rules/hs_deplete.rules"
include: config["binDir"]+"/pipes/rules/assembly.rules"
include: config["binDir"]+"/pipes/rules/interhost.rules"
include: config["binDir"]+"/pipes/rules/reports.rules"

ruleorder: revert_bam > merge_to_clean_bam > merge_one_per_sample

rule all:
    input:
        # create final assemblies for all samples
        expand("{dataDir}/{subdir}/{sample}.fasta",
            dataDir=config["dataDir"], subdir=config["subdirs"]["assembly"],
            sample=read_samples_file(config["samples_assembly"])),
        # create BAMs of aligned reads to own consensus and to common ref
        expand("{dataDir}/{subdir}/{sample}.bam",
            dataDir=config["dataDir"], subdir=config["subdirs"]["align_self"],
            sample=read_samples_file(config["samples_assembly"])),
        # create reference-guided analyses of diversity (quick outputs)
        os.path.join(config["dataDir"], config["subdirs"]["interhost"], 'ref_guided.fasta'),
        os.path.join(config["dataDir"], config["subdirs"]["interhost"], 'ref_guided.vcf.gz'),
        # create summary reports
        config["reportsDir"]+'/summary.coverage_ref.txt.gz',
        config["reportsDir"]+'/summary.coverage_self.txt.gz',
        config["reportsDir"]+'/summary.fastqc.txt',
        config["reportsDir"]+'/summary.bamstats.txt',
        config["reportsDir"]+'/summary.spike_count.txt'
    params: LSF="-N"
    run:
            if "job_profiler" in config:
                print("running report on all job runs")
                shell("{config[job_profiler]} {config[logDir]} {config[reportsDir]}/summary.job_stats.txt")
            print("echo all done!")

rule clean:
    params: LSF="-N"
    shell: "rm -rf {config[tmpDir]}/* .snakemake.tmp.*"
