"""
    This is a basic framework for depleting human and other contaminant 
    reads from NGS data.  All non-human reads should remain behind.
"""

__author__ = 'Kristian Andersen <andersen@broadinstitute.org>'

from snakemake.utils import makedirs

configfile: "config.json"

def read_samples_file(fname):
    with open(fname, 'rt') as inf:
        for line in inf:
            yield line.strip()

rule all_deplete:
    input:
      expand("{dataDir}/{subdir}/{sample}.{adjective}.bam",
        dataDir=config["dataDir"], subdir=config["subdirs"]["depletion"],
        sample=read_samples_file(config["samples_file"]),
        adjective=["cleaned", "bmtagger_depleted"])
    params: LSF="-N"
    shell:  "echo all done!"
    #shell:  "rm -rf {config[tmpDir]}/config[subdirs][depletion] && echo all done!"

rule bam_to_fastq:
    input:  config["dataDir"]+'/'+config["subdirs"]["source"]+'/{sample,\w+}.bam'
    output: config["tmpDir"]+'/'+config["subdirs"]["depletion"]+'/{sample}.raw.1.fastq', 
            config["tmpDir"]+'/'+config["subdirs"]["depletion"]+'/{sample}.raw.2.fastq', 
            config["tmpDir"]+'/'+config["subdirs"]["depletion"]+'/{sample}.raw.samheader.txt'
    params: LSF='-W 4:00 -M 6 -R "rusage[mem=3]"'
    run:
            makedirs(expand("{dir}/{subdir}",
                dir=[config["dataDir"],config["tmpDir"]],
                subdir=config["subdirs"]["depletion"]))
            shell("{config[binDir]}/read_utils.py bam_to_fastq {input} {output[0]} {output[1]} --outHeader {output[2]}")

rule deplete_bmtagger:
    input:  '{basename}.raw.1.fastq', '{basename}.raw.2.fastq'
    #       expand("{dir}/{db}.{suffix}",
    #           dir=config["bmTaggerDbDir"],
    #           db=config["bmTaggerDbs_remove"],
    #           suffix=["bitmask","srprism.idx","srprism.map"])
    output: '{basename}.bmtagger_depleted.1.fastq', '{basename}.bmtagger_depleted.2.fastq'
    params: LSF='-W 4:00 -M 16 -R "rusage[mem=8]"',
            refDbs = expand("{dir}/{db}", dir=config["bmTaggerDbDir"], db=config["bmTaggerDbs_remove"])
    shell:  "{config[binDir]}/taxon_filter.py partition_bmtagger {input[0]} {input[1]} {params.refDbs} --outNoMatch {output}"

rule rmdup_mvicuna:
    input:  '{basename}.bmtagger_depleted.1.fastq', '{basename}.bmtagger_depleted.2.fastq'
    output: '{basename}.rmdup.1.fastq', '{basename}.rmdup.2.fastq'
    params: LSF='-W 4:00 -M 16 -R "rusage[mem=8]"'
    shell:  "{config[binDir]}/taxon_filter.py dup_remove_mvicuna {input} {output}"

## Future optimization: add a partition_bmtagger step that pulls out
## a bunch of known microbial genomes that might comprise the bulk of the
## non-human reads. Run blastn only on the uncharacterized reads that
## do not match human or microbe by bmtagger. Afterwards, merge the
## microbe bmtagger hits with the blastn-depleted reads. This would just
## reduce the cpu time spent on blastn.

rule split_reads:
    input:  '{basename}.rmdup.{direction}.fastq'
    output: expand('{{basename}}.blast_split_input.{{direction}}.{split_id:04d}', split_id=range(config["blast_nchunks"]))
    shell:  "{config[binDir]}/read_utils.py split_reads {input} {wildcards.basename}.blast_split_input.{wildcards.direction}. --numChunks {config[blast_nchunks]} --suffixLen 4 --format fastq"

rule blastn_deplete:
    input:  '{prefix}.blast_split_input.{suffix}'
    output: '{prefix}.blast_split_output.{suffix}'
    params: LSF='-W 4:00 -M 8 -R "rusage[mem=4]" -sp 40',
            refDbs = expand("{dir}/{db}", dir=config["blastDbDir"], db=config["blastDb_remove"])
    shell:  "{config[binDir]}/taxon_filter.py deplete_blastn {input} {output} {params.refDbs}"

rule cat_reads:
    input:  expand('{{basename}}.blast_split_output.{{direction}}.{split_id:04d}', split_id=range(config["blast_nchunks"]))
    output: '{basename}.blast_depleted.{direction,[12]}.fastq'
    shell:  "cat {input} > {output}"

rule fastq_matepair_fix:
    input:  '{basename}.blast_depleted.1.fastq', '{basename}.blast_depleted.2.fastq'
    output: '{basename}.cleaned.1.fastq', '{basename}.cleaned.2.fastq'
    params: LSF='-W 4:00 -M 8 -R "rusage[mem=4]"'
    shell:  "{config[binDir]}/read_utils.py purge_unmated {input} {output}"

rule fastq_to_bam:
    input:  config["tmpDir"] +'/'+config["subdirs"]["depletion"]+'/{sample}.{adjective}.1.fastq',
            config["tmpDir"] +'/'+config["subdirs"]["depletion"]+'/{sample}.{adjective}.2.fastq',
            config["tmpDir"] +'/'+config["subdirs"]["depletion"]+'/{sample}.raw.samheader.txt'
    output: config["dataDir"]+'/'+config["subdirs"]["depletion"]+'/{sample,\w+}.{adjective,\w+}.bam'
    params: LSF='-W 4:00 -M 6 -R "rusage[mem=3]"'
    shell:  "{config[binDir]}/read_utils.py fastq_to_bam {input[0]} {input[1]} {output} --header {input[2]}"
