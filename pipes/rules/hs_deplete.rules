"""
    This is a basic framework for depleting human and other contaminant 
    reads from NGS data.  All non-human reads should remain behind.
"""

__author__ = 'Kristian Andersen <andersen@broadinstitute.org>'

configfile: "config.json"


rule all_deplete:
    # these are the final desired output files from this pipeline
    input:
      expand("{dataDir}/{sample}.{adjective}.bam",
        dataDir=config["dataDir"],
        sample=config["samples"],
        adjective=["cleaned", "bmtagger_depleted"])

rule bam_to_fastq:
    input:  config["dataDir"]+'/{sample,\w+}.bam'
    output: config["tmpDir"]+ '/{sample}.raw.1.fastq', 
            config["tmpDir"]+ '/{sample}.raw.2.fastq', 
            config["tmpDir"]+ '/{sample}.raw.samheader.txt'
    shell:  "{config[binDir]}/read_utils.py bam_to_fastq {input} {output[0]} {output[1]} --outHeader {output[2]}"

rule deplete_bmtagger:
    input:  '{basename}.raw.1.fastq', '{basename}.raw.2.fastq'
    output: '{basename}.bmtagger_depleted.1.fastq', '{basename}.bmtagger_depleted.2.fastq'
    params: LSF='-W 4:00',
            refDbs = expand("{dir}/{db}", dir=config["bmTaggerDbDir"], db=config["bmTaggerDbs_remove"])
    shell:  "{config[binDir]}/taxon_filter.py partition_bmtagger {input} {params.refDbs} --outNoMatch {output}"

rule rmdup_mvicuna:
    input:  '{basename}.bmtagger_depleted.1.fastq', '{basename}.bmtagger_depleted.2.fastq'
    output: '{basename}.rmdup.1.fastq', '{basename}.rmdup.2.fastq'
    params: LSF='-W 4:00 -R "rusage[mem=8]"'
    shell:  "{config[binDir]}/taxon_filter.py dup_remove_mvicuna {input} {output}"

rule split_reads:
    input:  '{basename}.rmdup.{direction}.fastq'
    output: dynamic('{basename}.blast_split_input.{direction}.{split_id}')
    params: length='100000'
    shell:  "split -a 3 -l {params.length} {input} {wildcards.basename}.blast_input.{wildcards.direction}."

rule blastn_deplete:
    input:  '{prefix}.blast_split_input.{suffix}'
    output: '{prefix}.blast_split_output.{suffix}'
    params: LSF='-q week', refDbs='{config[blastDbDir]}/{config[blastDb_remove]}'
    shell:  "{config[binDir]}/taxon_filter.py deplete_blastn {input} {output} {params.refDbs}"

rule cat_reads:
    input:  dynamic('{basename}.blast_split_output.{direction}.{split_id}')
    output: '{basename}.blast_depleted.{direction,[12]}.fastq'
    shell:  "cat {input} > {output}"

rule fastq_matepair_fix:
    input:  '{basename}.blast_depleted.1.fastq', '{basename}.blast_depleted.2.fastq'
    output: '{basename}.cleaned.1.fastq', '{basename}.cleaned.2.fastq'
    shell:  "{config[binDir]}/read_utils.py purge_unmated {input} {output}"

rule fastq_to_bam:
    input:  config["tmpDir"] +'/{sample}.{adjective}.1.fastq',
            config["tmpDir"] +'/{sample}.{adjective}.2.fastq',
            config["tmpDir"] +'/{sample}.raw.samheader.txt'
    output: config["dataDir"]+'/{sample,\w+}.{adjective,\w+}.bam'
    shell:  "{config[binDir]}/read_utils.py bam_to_fastq {input[0]} {input[1]} {output} --header {input[2]} --sampleName {wildcards.sample}"
